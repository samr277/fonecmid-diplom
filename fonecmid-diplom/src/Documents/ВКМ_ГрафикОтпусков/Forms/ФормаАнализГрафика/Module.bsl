

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	АдресДанныхДокумента = Параметры.АдресДанныхДокумента;
	ДанныеДокумента = ПолучитьИзВременногоХранилища(АдресДанныхДокумента);
	Год = ДанныеДокумента["Год"];
	ОтпускаСотрудников.Загрузить(ДанныеДокумента["ОтпускаСотрудников"]);
	
	// Вывод диаграммы Ганта
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.ОбластьЗаголовка.Текст = "График отпусков на " + Формат(Год, "ДФ=yyyy;") + " г.";
	Диаграмма.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Нет;
	Диаграмма.АвтоОпределениеПолногоИнтервала = Истина;
	Шкала = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы.Вставить(0);
	Шкала.Единица = ТипЕдиницыШкалыВремени.Неделя;
	
	Серия = Диаграмма.Серии.Добавить();
	
	Для Каждого Отпуск Из ОтпускаСотрудников Цикл
		Точка = Диаграмма.Точки.Добавить();
		Точка.Текст = Отпуск.Сотрудник;
		Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
		Интервал = Значение.Добавить();
		
		ГСЧ = Новый ГенераторСлучайныхЧисел(СтрДлина(Точка.Текст));
		Красный = ГСЧ.СлучайноеЧисло(50, 160);
		Зеленый = ГСЧ.СлучайноеЧисло(50, 200);
		Синий = ГСЧ.СлучайноеЧисло(120, 200);

		//@skip-check new-color
		Интервал.Цвет = Новый Цвет(Красный, Зеленый, Синий);
		Интервал.Начало = Отпуск.ДатаНачала;
		Интервал.Конец = КонецДня(Отпуск.ДатаОкончания);
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

#КонецОбласти
