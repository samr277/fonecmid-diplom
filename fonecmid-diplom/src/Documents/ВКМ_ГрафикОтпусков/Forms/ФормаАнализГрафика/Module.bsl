

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	АдресДанныхДокумента = Параметры.АдресДанныхДокумента;
	ДанныеДокумента = ПолучитьИзВременногоХранилища(АдресДанныхДокумента);
	Год = ДанныеДокумента["Год"];
	ОтпускаСотрудников.Загрузить(ДанныеДокумента["ОтпускаСотрудников"]);
	
	// Вывод диаграммы Ганта
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.ОбластьЗаголовка.Текст = "График отпусков на " + Формат(Год, "ДФ=yyyy;") + " г.";
	Диаграмма.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Нет;
	Диаграмма.АвтоОпределениеПолногоИнтервала = Истина;
	Шкала = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы.Вставить(0);
	Шкала.Единица = ТипЕдиницыШкалыВремени.Неделя;
	
	Серия = Диаграмма.Серии.Добавить();
	
	МассивЦветовСтиля = Новый Массив();
	МассивЦветовСтиля.Добавить(ЦветаСтиля.ДиаграммаГантаЦвет1);
	МассивЦветовСтиля.Добавить(ЦветаСтиля.ДиаграммаГантаЦвет2);
	МассивЦветовСтиля.Добавить(ЦветаСтиля.ДиаграммаГантаЦвет3);
	НомерЦвета = 0;
	Для Каждого Отпуск Из ОтпускаСотрудников Цикл
		Точка = Диаграмма.Точки.Добавить();
		Точка.Текст = Отпуск.Сотрудник;
		Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
		Интервал = Значение.Добавить();
		Интервал.Цвет = МассивЦветовСтиля[НомерЦвета];
		Интервал.Начало = Отпуск.ДатаНачала;
		Интервал.Конец = КонецДня(Отпуск.ДатаОкончания);
		НомерЦвета = (НомерЦвета+1)% МассивЦветовСтиля.Количество(); 
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

#КонецОбласти
