
#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьСообщениеВТелеграмЧат() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ИдентификаторТелеграмГруппы.Значение КАК ИдентификаторГруппы,
		|	ВКМ_ТокенТелеграмБота.Значение КАК ТокенБота,
		|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения
		|ИЗ
		|	Константа.ВКМ_ТокенТелеграмБота КАК ВКМ_ТокенТелеграмБота,
		|	Константа.ВКМ_ИдентификаторТелеграмГруппы КАК ВКМ_ИдентификаторТелеграмГруппы,
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
		|ГДЕ
		|	НЕ ВКМ_УведомленияТелеграмБоту.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", ТипКонтентаJSON());
	
	Пока Выборка.Следующий() Цикл
		АдресРесурса = "/bot" + Выборка.ТокенБота + "/sendMessage";
		
		ДанныеСообщения = Новый Структура;
		ДанныеСообщения.Вставить("chat_id", Выборка.ИдентификаторГруппы);
		ДанныеСообщения.Вставить("text", Выборка.ТекстСообщения);
				
		КодСостояния = ВыполнитьHTTPЗапрос("POST", АдресРесурса, СтрокаJSON(ДанныеСообщения), Заголовки);
		
		Если КодСостояния = 200 Тогда
			// Удаляем элемент справочника ВКМ_УведомленияТелеграмБоту
			ЭлементСправочника = Выборка.Ссылка.ПолучитьОбъект();
			ЭлементСправочника.Удалить();
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Функция ВыполнитьHTTPЗапрос(ИмяМетода, АдресРесурса, СтрокаJSON, Заголовки) Экспорт
	
	СерверИсточник = ТелеграмAPI();
			
	Попытка
		// Создать HTTP-соединение с сервером
		Соединение = Новый HTTPСоединение(СерверИсточник, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	Исключение
		ЗаписьЖурналаРегистрации("Не удалось соединиться с сервером: " + СерверИсточник, УровеньЖурналаРегистрации.Ошибка,,, 
								  ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки; 
	
	// Создать HTTP-запрос на основе URL
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	// Установить тело запроса из строки JSON
	Если ИмяМетода <> "DELETE" И ИмяМетода <> "GET" Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаJSON);
	КонецЕсли;
		
	Попытка
		// Получить ответ сервера в виде объекта HTTPОтвет
		HTTPОтвет = Соединение.ВызватьHTTPМетод(ИмяМетода, HTTPЗапрос);
	Исключение
		ЗаписьЖурналаРегистрации("Ошибка выполнения запроса.", УровеньЖурналаРегистрации.Ошибка,,, 
								  ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	КодСостояния = HTTPОтвет.КодСостояния;
	
	Если КодСостояния <> 200 Тогда
		ДанныеОбОшибке = ОбъектJSON(HTTPОтвет.ПолучитьТелоКакСтроку());
		КодОшибки = СтрШаблон("Код ошибки: %1.", ДанныеОбОшибке.error_code);
		ЗаписьЖурналаРегистрации(КодОшибки, УровеньЖурналаРегистрации.Ошибка,,, ДанныеОбОшибке.description);
	КонецЕсли;
	
	Возврат КодСостояния;
	
КонецФункции

Функция ТелеграмAPI() Экспорт
	Возврат "api.telegram.org";
КонецФункции

Функция ТипКонтентаJSON() Экспорт
	Возврат "application/json";
КонецФункции

Функция СтрокаJSON(Объект) Экспорт
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(Новый ПараметрыЗаписиJSON(, Символы.Таб));
	ЗаписатьJSON(Запись, Объект);
	Строка = Запись.Закрыть();
	
	Возврат Строка;
КонецФункции

Функция ОбъектJSON(СтрокаJSON) Экспорт
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(СтрокаJSON);
	Объект = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();
	
	Возврат Объект;
КонецФункции

#КонецОбласти
